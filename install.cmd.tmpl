@   echo off

:main /? | [/v] [/n] [prefix directory] [/cmd-lib directory]

:: = DESCRIPTION
:: =   @PROG_NAME@ - installs !PACKAGE!.
:: =
:: = OPTIONS
:: =   /v       Be verbose. Repeat for extra verbosity.
:: =   /n       Dry-run. Do not install, just show commands.
:: =   /prefix   Name of directory to install !PACKAGE! in.
:: =             Default is !prefix!.
:: =   /cmd-lib  Directory with cmd-lib.
:: =             Default is !cmdlib!.

:: @author Jan Bruun Andersen
:: @version @(#) Version: 2015-12-05

    verify 2>NUL: other
    setlocal EnableExtensions
    if ErrorLevel 1 (
	echo Error - Unable to enable extensions.
	goto :EOF
    )

    if /i "%~1" == "/trace" shift & prompt $G$G & echo on

:defaults
    set "PROG_FULL=%~f0"
    set "PACKAGE=cmd-installers"

    set "show_help=false"
    set "verbosity=0"
    set "dry_run=false"
    set "prefix=@DST_DIR@"
    set "cmdlib=@CMD_LIB@"

    set "cnt_mkdir=0"
    set "cnt_copy=0"

:getopts
    if /i "%~1" == "/?"		set "show_help=true"	& shift		& goto :getopts

    if /i "%~1" == "/v"		set /a "verbosity+=1"	& shift		& goto :getopts
    if /i "%~1" == "/n"		set "dry_run=true"	& shift		& goto :getopts
    if /i "%~1" == "/prefix"	set "prefix=%~2"	& shift & shift	& goto :getopts
    if /i "%~1" == "/cmd-lib"	set "cmdlib=%~2"	& shift & shift	& goto :getopts

    for %%F in (cl_init.cmd) do if "" == "%%~$PATH:F" set "PATH=%cmdlib%;%PATH%"
    call cl_init "%PROG_FULL%" || (echo Failed to initialise cmd-lib. & goto :exit)

    set "char1=%~1"
    set "char1=%char1:~0,1%"
    if "%char1%" == "/" (
	echo Unknown option - %1.
	echo.
	call cl_usage "%PROG_FULL%"
	goto :error_exit
    )

    if "%show_help%" == "true" call cl_help "%PROG_FULL%" & goto :EOF

    if not "%~1" == "" (
	echo Extra argument - %1.
	echo.
    	call cl_usage "%PROG_FULL%"
	goto :error_exit
    )

    if not defined prefix (
	echo /prefix directory not defined.
	echo.
    	call cl_usage "%PROG_FULL%"
	goto :error_exit
    )

    if 0%verbosity% geq 2 (
	echo dry-run = %dry_run%
	echo extras  = %extras%
	echo action  = %action%
	echo prefix  = %prefix%
	echo cmdlib  = %cmdlib%
	echo.
    )

    rem .----------------------------------------------------------------------
    rem | This is where the real fun begins!
    rem '----------------------------------------------------------------------

    if not exist "%prefix%"	       call :mkdir "%prefix%"	    || goto :exit

    rem %%F in (README.md)	    do call :copy "%%F" "%prefix%" || goto :exit
    rem %%F in (LICENSE)	    do call :copy "%%F" "%prefix%" || goto :exit
    for %%F in (src\*.cmd)	    do call :copy "%%F" "%prefix%" || goto :exit
    rem %%F in (src\*.tmpl)	    do call :copy "%%F" "%prefix%" || goto :exit

    if 0%cnt_mkdir% gtr 0 echo Directories created: %cnt_mkdir%
    if 0%cnt_copy%  gtr 0 echo Files copied:        %cnt_copy%

    goto :exit
goto :EOF

:mkdir dir
    if "%dry_run%" == "true" echo mkdir "%~1" & goto :EOF

    if 0%verbosity% geq 1 echo Creating directory "%~1".
    mkdir "%~1" && (
	set /a "cnt_mkdir+=1"
    )
goto :EOF

:copy file dst-dir
    if "%dry_run%" == "true" echo copy "%~1" "%~2" & goto :EOF

    if 0%verbosity% geq 1 echo Copying "%~1" to "%~2".
    copy "%~1" "%~2" >NUL: && (
	set /a "cnt_copy+=1"
    ) || (
	echo ERROR - Failed to copy "%~1" to "%~2".
	copy "%~1" "%~2"
	goto :exit
    ) >&2
goto :EOF

rem .--------------------------------------------------------------------------
rem | Displays a selection of variables belonging to this script.
rem | Very handy when debugging.
rem '--------------------------------------------------------------------------
:dump_variables
    echo =======
    echo cwd            = "%CD%"
    echo tmp_dir        = "%tmp_dir%"

    echo show_help      = "%show_help%"
    echo verbosity      = "%verbosity%"
    echo dry_run        = "%dry_run%"
    echo prefix         = "%prefix%"
    echo cmdlib         = "%cmdlib%"

    echo cnt_mkdir      = "%cnt_mkdir%"
    echo cnt_copy       = "%cnt_copy%"

    if defined tmp_dir if exist "%tmp_dir%\" (
	echo.
	dir %tmp_dir%
    )

    echo =======
goto :EOF

rem ----------------------------------------------------------------------------
rem Sets ErrorLevel and exit-status. Without a proper exit-status tests like
rem 'command && echo Success || echo Failure' will not work,
rem
rem OBS: NO commands must follow the call to %ComSpec%, not even REM-arks,
rem      or the exit-status will be destroyed. However, null commands like
rem      labels (or ::) is okay.
rem ----------------------------------------------------------------------------
:no_error
    time >NUL: /t	& rem Set ErrorLevel = 0.
    goto :exit
:error_exit
    verify 2>NUL: other	& rem Set ErrorLevel = 1.
:exit
    %ComSpec% /c exit %ErrorLevel%

:: vim: set filetype=dosbatch tabstop=8 softtabstop=4 shiftwidth=4 noexpandtab:
:: vim: set foldmethod=indent
